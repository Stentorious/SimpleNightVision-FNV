ref rOwner
string_var sTimerName

int iSize
int iCount
float fEnergy
float fEnergyUse
ref rEnergyItem

begin Function {sTimerName, rOwner}

	if eval GetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyRequired" == 0
		return
	endif

	; Update energy charge
	fEnergyUse = GetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyUseCalc"
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyCharge" (fEnergy = Goo1.AuxVarGetFlt "SNV_Mode" 2)
	Goo1.AuxVarSetFlt "SNV_Mode" (GetMaxOf 0 (fEnergy - fEnergyUse)) 2

	; Update Energy Display text
	if eval GetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_ChargeStyle" == 1
		sTimerName = sv_Construct "%g%%" (Flr (((fEnergy / (Goo1.AuxVarGetFlt "SNV_Mode" 3)) * 100) + 0.5))
	elseif eval fEnergyUse > 0
		sTimerName = sv_Construct "%gs" (Flr ((fEnergy / fEnergyUse) + 0.5))
	else
		sTimerName = "-"
	endif
	SetUIStringAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\ChargeText\string" "%z" sTimerName
	sv_Destruct sTimerName

	if eval fEnergy > 0
		return
	endif

	; Energy consumption
	iSize = Goo1.AuxVarSize "*SNV_Energy"
	while (iSize -= 2) > -1
		if eval (iCount = Player.GetItemCount (rEnergyItem = Goo1.AuxVarGetRef "*SNV_Energy" iSize)) > 0
			break
		endif
	loop
	SetUIStringAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\ItemName\string" "%z" (GetAmmoName 1 rEnergyItem)
	SetUIStringAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\CountText\string" "%g" iCount
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyCount" iCount
	SetUIFloatGradual "HUDMainMenu\ModernNightVision\EnergyDisplay\CountText\_Alpha" 510
	SetUIFloatGradual "HUDMainMenu\ModernNightVision\EnergyDisplay\CountText\_Alpha" 510 0 1
	if eval iCount > 0
		Player.RemoveItem rEnergyItem 1 1
		Player.AddItemAlt (GetAmmoCasing rEnergyItem) 1
		Goo1.AuxVarSetFlt "SNV_Mode" (fEnergy = Goo1.AuxVarGetFlt "*SNV_Energy" (iSize + 1)) 2
		Goo1.AuxVarSetFlt "SNV_Mode" fEnergy 3
		SetUIFloatGradual "HUDMainMenu\ModernNightVision\_EnergyCharge" 0 fEnergy 1
		SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyMax" fEnergy
		PlaySound WPNPistolLaserCharge
	else
		AuxTimerStop "*SNV_KeyToggle"
		Call (CompileScript "ModernNightVision/ToggleOff.gek") 1
		;MessageExAlt 3 "#0|You have no item to charge your vision modes."
		PlaySound OBJComputerTerminalPowerDown
	endif

end
