; Modern Night Vision - Stentorious

; Aux Vars
; "SNV_Mode"
; 0 = Vision mode On/Off state
; 1 = Vision mode type
; 2 = Remaining energy
; 3 = Loaded energy max charge

; "*SNV_INI"
; 0 = Keybind
; 1 = Controller support
; 2 = Pip-Boy overlay opacity mul
; 3 = Cyberware support
; 4 = Vision mode switch behavior
; 5 = First person anims
; 6 = Night vision enabled for current helmet
; 7 = Thermal vision enabled for current helmet

; "*SNV_Override"
; 0 = Color - Red (Night Vision)
; 1 = Color - Red (Night Vision)
; 2 = Color - Red (Night Vision)
; 3 = Color - Systemcolor (Night Vision)
; 4 = Overall brightness (Night Vision)
; 5 = Energy use multiplier (Night Vision)
; 6 = Color - Red (Thermal Vision)
; 7 = Color - Red (Thermal Vision)
; 8 = Color - Red (Thermal Vision)
; 9 = Color - Systemcolor (Thermal Vision)
; 10 = Overall brightness (Thermal Vision)
; 11 = Energy use multiplier (Thermal Vision)
; 12 = Overlay Texture (Night Vision)
; 13 = Overlay Texture (Thermal Vision)

Goo1.AuxVarSetFlt "*SNV_INI" 0 0
Goo1.AuxVarSetFlt "*SNV_INI" 0 1
Goo1.AuxVarSetFlt "*SNV_INI" 0 2
Goo1.AuxVarSetFlt "*SNV_INI" 0 3
Goo1.AuxVarSetFlt "*SNV_INI" 0 4
Goo1.AuxVarSetFlt "*SNV_INI" 0 5
Goo1.AuxVarSetFlt "*SNV_INI" 0 6
Goo1.AuxVarSetFlt "*SNV_INI" 0 7
Goo1.AuxVarSetFlt "*SNV_INI" 0 8
Goo1.AuxVarSetFlt "*SNV_INI" 0 9
Goo1.AuxVarSetFlt "*SNV_INI" 0 10
Goo1.AuxVarSetFlt "*SNV_INI" 0 11

; Check for requirements
if eval IsModLoaded "Enhanced Vision.esp" || IsModLoaded "Advanced Recon Tech.esm"
	MessageBoxEx "Conflicting Night Vision mod detected!%rModern Night Vision has been disabled."
	return
endif
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 360
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 165
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif
if GetPluginVersion "kNVSE" < 37
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest kNVSE plugin."
	return
endif
if GetPluginVersion "lStewieAl's Tweaks" < 906
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest lStewieAl's Tweaks."
	return
endif
if eval FileExists "UIO\Public\RadiationVisuals.txt" && FileExists "Textures\Interface\Stentorious\RadiationVisuals\0_0.dds" == 0
	MessageBoxEx "Modern Night Vision texture preload error.%rUpdate Radiation Visuals to latest version."
	return
endif
Goo1.AuxVarSetFlt "*SNV_Init" 1 0

string_var sINIPath = "Stentorious\ModernNightVision.ini"

; Init static animation
SetUIFloatGradual "HUDMainMenu\ModernNightVision\Static\_index" 0 9.999 0.5 3

; Hazmat darklight cowl support
if eval GetObjectEffect NVDLC03GhostHoodlessHead == NVDLC03GhostHoodGogglesOE
	SetObjectEffect NVDLC03GhostHoodlessHead
	AssignKeyword NVDLC03GhostHoodlessHead "ModernNightVision"
endif

; Riot gear support
ref rHelmet = GetObjectEffect NVDLC04HelmetRiotGear
if rHelmet == NVDLC04HelmetRiotGearOE
	if GetNthEffectBase rHelmet 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 3
	endif
	if GetNthEffectBase rHelmet 2 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 2
	endif
	if GetNthEffectBase rHelmet 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 0
	endif
endif
AssignKeyword NVDLC04HelmetRiotGear "ModernNightVision"

; Advanced riot gear support
rHelmet = GetObjectEffect NVDLC04HelmetRiotGearCustom
if rHelmet == NVDLC04HelmetRiotGearCustomOE
	if GetNthEffectBase rHelmet 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 3
	endif
		if GetNthEffectBase rHelmet 2 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 2
	endif
	if GetNthEffectBase rHelmet 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 0
	endif
endif
AssignKeyword NVDLC04HelmetRiotGearCustom "ModernNightVision"

; Elite riot gear support
rHelmet = GetObjectEffect NVDLC04HelmetRiotGearUnique
if rHelmet == NVDLC04HelmetRiotGearUniqueOE
	if GetNthEffectBase rHelmet 4 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 4
	endif
		if GetNthEffectBase rHelmet 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 3
	endif
	if GetNthEffectBase rHelmet 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 0
	endif
endif
AssignKeyword NVDLC04HelmetRiotGearUnique "ModernNightVision"

; TTW Night vision goggles support
if eval IsModLoaded "TaleOfTwoWastelands.esm"
	rHelmet = EditorIDToFormID "TTWNightVisionGoggles"
	if eval IsScripted rHelmet
		RemoveScript rHelmet
		AssignKeyword rHelmet "ModernNightVision"
	endif
	rHelmet = EditorIDToFormID "TTWReinforcedNightVisionGoggles"
	if eval IsScripted rHelmet
		RemoveScript rHelmet
		AssignKeyword rHelmet "ModernNightVision"
	endif
endif

; Vision mode
AuxVarSetFlt "*MNV_NV" (eval GetINIFloat_Cached "NightVision:bEnable" (sINIPath) 1) 0 Caps001
AuxVarSetFlt "*MNV_TV" (eval GetINIFloat_Cached "ThermalVision:bEnable" (sINIPath) 1) 0 Caps001

; Energy use multiplier
AuxVarSetFlt "*MNV_NV" (GetMaxOf 0 (GetINIFloat_Cached "NightVision:fEnergyConsumption" (sINIPath) 1)) 1 Caps001
AuxVarSetFlt "*MNV_TV" (GetMaxOf 0 (GetINIFloat_Cached "ThermalVision:fEnergyConsumption" (sINIPath) 1.5)) 1 Caps001

; Color
AuxVarSetFlt "*MNV_NV" (eval GetINIFloat_Cached "NightVision:bColorTint" (sINIPath) 0) 2 Caps001
AuxVarSetFlt "*MNV_NV" (GetINIFloat_Cached "NightVision:iRed" (sINIPath) 127) 3 Caps001
AuxVarSetFlt "*MNV_NV" (GetINIFloat_Cached "NightVision:iGreen" (sINIPath) 255) 4 Caps001
AuxVarSetFlt "*MNV_NV" (GetINIFloat_Cached "NightVision:iBlue" (sINIPath) 127) 5 Caps001
AuxVarSetFlt "*MNV_TV" (eval GetINIFloat_Cached "ThermalVision:bColorTint" (sINIPath) 0) 2 Caps001
AuxVarSetFlt "*MNV_TV" (GetINIFloat_Cached "ThermalVision:iRed" (sINIPath) 255) 3 Caps001
AuxVarSetFlt "*MNV_TV" (GetINIFloat_Cached "ThermalVision:iGreen" (sINIPath) 255) 4 Caps001
AuxVarSetFlt "*MNV_TV" (GetINIFloat_Cached "ThermalVision:iBlue" (sINIPath) 255) 5 Caps001

; Static Noise
AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fStaticStrength" (sINIPath) 0.25) 0 0.75) 6 Caps001
AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fStaticStrength" (sINIPath) 0.275) 0 0.75) 6 Caps001

; Brightness (Overall)
AuxVarSetFlt "*MNV_NV" (GetMaxOf 1 (GetINIFloat_Cached "NightVision:fOverallBrightness" (sINIPath) 2.5)) 7 Caps001
AuxVarSetFlt "*MNV_TV" (GetMaxOf 1 (GetINIFloat_Cached "ThermalVision:fOverallBrightness" (sINIPath) 1)) 7 Caps001

; Brightness (Sky)
AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fMoonlightBrightness" (sINIPath) 1) 0 10) 8 Caps001
AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fMoonlightBrightness" (sINIPath) 0) 0 10) 8 Caps001

; Motion blur
AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fMotionBlur" (sINIPath) 0.1) 0 4) 9 Caps001
AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fMotionBlur" (sINIPath) 2) 0 4) 9 Caps001

; Depth of Field
AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fDepthBlur" (sINIPath) 5.75) 0 10) 10 Caps001
AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fDepthBlur" (sINIPath) 5.75) 0 10) 10 Caps001

; Overlay (Opacity)
AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fOverlayOpacity" (sINIPath) 1) 0 100) 11 Caps001
AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fOverlayOpacity" (sINIPath) 1) 0 100) 11 Caps001

; Overlay (Scale)
AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fOverlayScale" (sINIPath) 1.1) 1 1.5) 12 Caps001
AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fOverlayScale" (sINIPath) 1.1) 1 1.5) 12 Caps001

; Overlay (Texture)
AuxVarSetStr "*MNV_NV" "night_vision" 13 Caps001
AuxVarSetStr "*MNV_TV" "thermal_vision" 13 Caps001

; IMOD traits
; Default traits
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 9 1			; HDR: Emissive Mult (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 11 1			; HDR: Target LUM (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 13 1			; HDR: Upper LUM Clamp (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 15 1			; HDR: Bright Scale (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 25 1			; HDR: Sunlight Dimmer (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 37 1			; Cinematic: Saturation (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 39 1			; Cinematic: Contrast (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 43 1			; Cinematic: Brightness (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 55 0			; Radial Blur: Strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 56 0			; Radial Blur: Rampup
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 57 0			; Radial Blur: Up Start
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 50 255		; Cinematic: Tint: Alpha
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 67 1			; Depth of Field: Mode (Front)

; Mode specific
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 10 5			; HDR: Emissive Mult (Add) 			Emissive bloom strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 16 1			; HDR: Bright Scale (Add) 			General bloom strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 17 0.75		; HDR: Bright Clamp (Multiply) 			Prevents overblown bloom
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 19 1			; HDR: LUM Ramp No Tex (Multiply) 	Sky Brightness
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 39 0.1;25		; Cinematic: Contrast (Multiply) 	Decrease color contrast
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 42 1			; Cinematic: Contrast Avg Lum (Add) Increase brightness contrast

; IMOD traits
; Default traits
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 9 1				; HDR: Emissive Mult (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 11 1			; HDR: Target LUM (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 13 1			; HDR: Upper LUM Clamp (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 15 1			; HDR: Bright Scale (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 25 1			; HDR: Sunlight Dimmer (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 37 1			; Cinematic: Saturation (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 39 1			; Cinematic: Contrast (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 43 1			; Cinematic: Brightness (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 55 0			; Radial Blur: Strength
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 56 0			; Radial Blur: Rampup
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 57 0			; Radial Blur: Up Start
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 50 255			; Cinematic: Tint: Alpha
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 67 1			; Depth of Field: Mode (Front)

; Mode specific
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 10 5			; HDR: Emissive Mult (Add) 			Emissive bloom strength
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 16 1			; HDR: Bright Scale (Add) 			General bloom strength
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 17 1.0			; HDR: Bright Clamp (Multiply) 		Prevents overblown bloom
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 19 0.75			; HDR: LUM Ramp No Tex (Multiply) 	Sky Brightness
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 27 0			; HDR: Grass Dimmer (Multiply) 		Grass brightness
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 39 0.125		; Cinematic: Contrast (Multiply) 	Decrease color contrast
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 42 1			; Cinematic: Contrast Avg Lum (Add) Increase brightness contrast

; Thermal vision shader traits
SetEffectShaderTraitNumeric effectAbsorb 0 24					; Flags
SetEffectShaderTraitNumeric effectAbsorb 4 16777215				; Fill/Texture Effect - Color (RGB)
SetEffectShaderTraitNumeric effectAbsorb 8 1					; Fill/Texture Effect - Presistent Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 10 0					; Fill/Texture Effect - Alpha Pulse Frequency
SetEffectShaderTraitNumeric effectAbsorb 21 1					; Fill/Texture Effect - Full Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 13 1					; Edge Effect - Fall Off
SetEffectShaderTraitNumeric effectAbsorb 18 1					; Fill/Texture Effect - Presistent Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 20 0					; Edge Effect - Alpha Pulse Frequency
SetEffectShaderTraitNumeric effectAbsorb 22 1					; Edge Effect - Full Alpha Ratio

; No edge effect, more consistent
SetEffectShaderTraitNumeric effectAbsorb 1 10					; Membrane Shader - Source Blend Mode
SetEffectShaderTraitNumeric effectAbsorb 23 2					; Membrane Shader - Dest Blend Mode
SetEffectShaderTraitNumeric effectAbsorb 14 16777215			; Edge Effect - Color (RGB)

; With edge effect, better looking, edge effect can bug out and disappear
;SetEffectShaderTraitNumeric effectAbsorb 1 10
;SetEffectShaderTraitNumeric effectAbsorb 23 2
;SetEffectShaderTraitNumeric effectAbsorb 14 3355443

;Set event handlers
SetEventHandler "OnCellEnter" (CompileScript "ModernNightVision\OnCellEnter.gek")
SetOnMenuOpenEventHandler (CompileScript "ModernNightVision\OnMenuOpen.gek") 1 1002
SetOnMenuOpenEventHandler (CompileScript "ModernNightVision\OnMenuOpen.gek") 1 1003
SetOnMenuOpenEventHandler (CompileScript "ModernNightVision\OnMenuOpen.gek") 1 1023
SetOnMenuCloseEventHandler (CompileScript "ModernNightVision\OnMenuClose.gek") 1 1002
SetOnMenuCloseEventHandler (CompileScript "ModernNightVision\OnMenuClose.gek") 1 1003
SetOnMenuCloseEventHandler (CompileScript "ModernNightVision\OnMenuClose.gek") 1 1023
SetOnMenuOpenEventHandler (CompileScript "ModernNightVision\MCMExtender\OnMenuOpen.gek") 1013
SetEventHandler "OnActorEquip" (CompileScript "ModernNightVision\OnEquip.gek") "first"::Player
SetEventHandler "OnActorUnequip" (CompileScript "ModernNightVision\OnUnequip.gek") "first"::Player

; MCM Extender event
SetEventHandler "MCMExtUpdate" (CompileScript "ModernNightVision\MCMExtender\UpdateMenu.gek")

; KB&M keybind
Goo1.AuxVarSetFlt "*SNV_INI" (Flr (GetMaxOf 0 (GetINIFloat_Cached "General:iKeybind" (sINIPath) 49))) 0
; Controller support
Goo1.AuxVarSetFlt "*SNV_INI" (eval GetINIFloat_Cached "General:bControllerSupport" (sINIPath) 1) 1
SetEventHandler "OnControlDown:27" (begin function {int iKeyCode}
	if eval GetController == 0 || Goo1.AuxVarGetFlt "*SNV_INI" 1 == 0
		return
	endif
	DisableButton 1
end)
SetEventHandler "OnControlUp:27" (begin function {int iKeyCode}
	if eval GetController == 0 || Goo1.AuxVarGetFlt "*SNV_INI" 1 == 0
		return
	endif
	EnableButton 1
end)
SetEventHandler "OnButtonDown:1" (begin function {int iKeyCode}
	if eval GetController == 0 || IsControlPressed 27 == 0 || Goo1.AuxVarGetFlt "*SNV_INI" 1 == 0
		return
	endif
	Call (CompileScript "ModernNightVision\OnKeyDown.gek")
end)
SetEventHandler "OnButtonUp:1" (begin function {int iKeyCode}
	if eval GetController == 0 || Goo1.AuxVarGetFlt "*SNV_INI" 1 == 0
		return
	endif
	Call (CompileScript "ModernNightVision\OnKeyUp.gek")
end)

; Cyberware support
if eval IsModLoaded "Cyberware.esm" && GetINIFloat_Cached "ModSupport:bOverrideCyberware" (sINIPath) == 1
	SetOnRemovePerkEventHandler 1 (CompileScript "ModernNightVision\OnRemovePerkCyb.gek") 0 (EditorIDToFormID "CWxImplantVisionModePerk")
	SetOnQuestStageEventHandler (CompileScript "ModernNightVision\OnQuestStageCyb.gek") 1 (EditorIDToFormID "CWxBionicEyesQuest") 1 1
	Goo1.AuxVarSetFlt "*SNV_INI" 1 3
endif

Goo1.AuxVarSetFlt "*SNV_INI" (Clamp (GetINIFloat_Cached "General:fPipBoyStrength" (sINIPath) 50) 0 100) 2
Goo1.AuxVarSetFlt "*SNV_INI" (eval GetINIFloat_Cached "General:iSwitchMode" (sINIPath) 0) 4			; Vision mode control
Goo1.AuxVarSetFlt "*SNV_INI" (eval GetINIFloat_Cached "General:b1stPersonAnimations" (sINIPath) 1) 5	; Animation support
SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyRequired" (eval GetINIFloat_Cached "General:bRequireEnergy" (sINIPath) 1)		; Energy usage

; Energy display UI
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_ItemName" (GetINIFloat_Cached "EnergyDisplay:bShowItemName" (sINIPath) 1)
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_IconScale" (GetINIFloat_Cached "EnergyDisplay:fScale" (sINIPath) 1)
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_OffsetX" (GetINIFloat_Cached "EnergyDisplay:fOffsetX" (sINIPath) 0)
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_OffsetY" (GetINIFloat_Cached "EnergyDisplay:fOffsetY" (sINIPath) 0)
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_IconStyle" (eval GetINIFloat_Cached "EnergyDisplay:iIconStyle" (sINIPath) 1)
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_ChargeStyle" (Clamp (GetINIFloat_Cached "EnergyDisplay:iChargeStyle" (sINIPath) 1) 0 2)
if eval IsModLoaded "DarNifiedUINV.esp"
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\ChargeText\font" 6
	ModUIFloat "HUDMainMenu\ModernNightVision\EnergyDisplay\ChargeText\x" (3 * (GetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_IconScale"))
endif

; Overlay inertia
SetUIFloatAlt "HUDMainMenu\ModernNightVision\_Inertia" (Clamp (GetINIFloat_Cached "General:fOverlayInertia" (sINIPath) 1) 0 2)

; Energy consumption
array_var aOverrideINIs = GetINISection "EnergyConsumption" (sINIPath)
if eval (ar_Size aOverrideINIs) < 2
	aOverrideINIs = ar_Construct "stringmap"
	aOverrideINIs["0"] = "AmmoSmallEnergyCell|300"
endif
foreach array_var aForE <- aOverrideINIs
	array_var aTemp = Sv_Split (*aForE) "|"
	if eval IsFormValid (rHelmet = EditorIDToFormID (aTemp[0])) == 0 || GetType rHelmet != 41
		continue
	endif
	Goo1.AuxVarSetRef "*SNV_Energy" rHelmet -1
	Goo1.AuxVarSetFlt "*SNV_Energy" (#(aTemp[1])) -1
loop

CloseFileSO (sINIPath) 0

; Config override support
string_var sTemp = ""
aOverrideINIs = GetFilesInFolder "Config\Stentorious\ModernNightVision" "*.ini"
if eval (int iSize = Ar_Size aOverrideINIs) > 0
	while (iSize -= 1) > -1
		sTemp = aOverrideINIs[iSize]
		if eval IsBaseForm (rHelmet = EditorIDToFormID (sTemp[0:-5])) == 0
			continue
		endif

		; Override keyword
		AssignKeyword rHelmet "SNV_Override"
		AssignKeyword rHelmet "ModernNightVision"

		sINIPath = "Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])

		; Vision mode
		AuxVarSetFlt "*MNV_NV" (eval GetINIFloat_Cached "NightVision:bEnable" (sINIPath) (AuxVarGetFlt "*MNV_NV" 0 Caps001)) 0 rHelmet
		AuxVarSetFlt "*MNV_TV" (eval GetINIFloat_Cached "ThermalVision:bEnable" (sINIPath) (AuxVarGetFlt "*MNV_TV" 0 Caps001)) 0 rHelmet

		; Energy use multiplier
		AuxVarSetFlt "*MNV_NV" (GetMaxOf 0 (GetINIFloat_Cached "NightVision:fEnergyConsumption" (sINIPath) (AuxVarGetFlt "*MNV_NV" 1 Caps001))) 1 rHelmet
		AuxVarSetFlt "*MNV_TV" (GetMaxOf 0 (GetINIFloat_Cached "ThermalVision:fEnergyConsumption" (sINIPath) (AuxVarGetFlt "*MNV_TV" 1 Caps001))) 1 rHelmet

		; Color
		AuxVarSetFlt "*MNV_NV" (eval GetINIFloat_Cached "NightVision:bColorTint" (sINIPath) (AuxVarGetFlt "*MNV_NV" 2 Caps001)) 2 rHelmet
		AuxVarSetFlt "*MNV_NV" (GetINIFloat_Cached "NightVision:iRed" (sINIPath) (AuxVarGetFlt "*MNV_NV" 3 Caps001)) 3 rHelmet
		AuxVarSetFlt "*MNV_NV" (GetINIFloat_Cached "NightVision:iGreen" (sINIPath) (AuxVarGetFlt "*MNV_NV" 4 Caps001)) 4 rHelmet
		AuxVarSetFlt "*MNV_NV" (GetINIFloat_Cached "NightVision:iBlue" (sINIPath) (AuxVarGetFlt "*MNV_NV" 5 Caps001)) 5 rHelmet
		AuxVarSetFlt "*MNV_TV" (eval GetINIFloat_Cached "ThermalVision:bColorTint" (sINIPath) (AuxVarGetFlt "*MNV_TV" 2 Caps001)) 2 rHelmet
		AuxVarSetFlt "*MNV_TV" (GetINIFloat_Cached "ThermalVision:iRed" (sINIPath) (AuxVarGetFlt "*MNV_TV" 3 Caps001)) 3 rHelmet
		AuxVarSetFlt "*MNV_TV" (GetINIFloat_Cached "ThermalVision:iGreen" (sINIPath) (AuxVarGetFlt "*MNV_TV" 4 Caps001)) 4 rHelmet
		AuxVarSetFlt "*MNV_TV" (GetINIFloat_Cached "ThermalVision:iBlue" (sINIPath) (AuxVarGetFlt "*MNV_TV" 5 Caps001)) 5 rHelmet

		; Static Noise
		AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fStaticStrength" (sINIPath) (AuxVarGetFlt "*MNV_NV" 6 Caps001)) 0 0.75) 6 rHelmet
		AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fStaticStrength" (sINIPath) (AuxVarGetFlt "*MNV_TV" 6 Caps001)) 0 0.75) 6 rHelmet

		; Brightness (Overall)
		AuxVarSetFlt "*MNV_NV" (GetMaxOf 1 (GetINIFloat_Cached "NightVision:fOverallBrightness" (sINIPath) (AuxVarGetFlt "*MNV_NV" 7 Caps001))) 7 rHelmet
		AuxVarSetFlt "*MNV_TV" (GetMaxOf 1 (GetINIFloat_Cached "ThermalVision:fOverallBrightness" (sINIPath) (AuxVarGetFlt "*MNV_TV" 7 Caps001))) 7 rHelmet

		; Brightness (Sky)
		AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fMoonlightBrightness" (sINIPath) (AuxVarGetFlt "*MNV_NV" 8 Caps001)) 0 10) 8 rHelmet
		AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fMoonlightBrightness" (sINIPath) (AuxVarGetFlt "*MNV_TV" 8 Caps001)) 0 10) 8 rHelmet

		; Motion blur
		AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fMotionBlur" (sINIPath) (AuxVarGetFlt "*MNV_NV" 9 Caps001)) 0 4) 9 rHelmet
		AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fMotionBlur" (sINIPath) (AuxVarGetFlt "*MNV_TV" 9 Caps001)) 0 4) 9 rHelmet

		; Depth of Field
		AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fDepthBlur" (sINIPath) (AuxVarGetFlt "*MNV_NV" 10 Caps001)) 0 10) 10 rHelmet
		AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fDepthBlur" (sINIPath) (AuxVarGetFlt "*MNV_TV" 10 Caps001)) 0 10) 10 rHelmet

		; Overlay (Opacity)
		AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fOverlayOpacity" (sINIPath) (AuxVarGetFlt "*MNV_NV" 11 Caps001)) 0 100) 11 rHelmet
		AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fOverlayOpacity" (sINIPath) (AuxVarGetFlt "*MNV_TV" 11 Caps001)) 0 100) 11 rHelmet

		; Overlay (Scale)
		AuxVarSetFlt "*MNV_NV" (Clamp (GetINIFloat_Cached "NightVision:fOverlayScale" (sINIPath) (AuxVarGetFlt "*MNV_NV" 12 Caps001)) 1 1.5) 12 rHelmet
		AuxVarSetFlt "*MNV_TV" (Clamp (GetINIFloat_Cached "ThermalVision:fOverlayScale" (sINIPath) (AuxVarGetFlt "*MNV_TV" 12 Caps001)) 1 1.5) 12 rHelmet

		; Overlay (Texture)
		AuxVarSetStr "*MNV_NV" (GetINIString_Cached "NightVision:sOverlayTexture" (sINIPath) (AuxVarGetStr "*MNV_NV" 13 Caps001)) 13 rHelmet
		AuxVarSetStr "*MNV_TV" (GetINIString_Cached "ThermalVision:sOverlayTexture" (sINIPath) (AuxVarGetStr "*MNV_TV" 13 Caps001)) 13 rHelmet

		; Close INI cache
		CloseFileSO (sINIPath) 0

	loop
endif

aOverrideINIs = aTemp = aForE = Ar_Null
Sv_Destruct sINIPath sTemp
