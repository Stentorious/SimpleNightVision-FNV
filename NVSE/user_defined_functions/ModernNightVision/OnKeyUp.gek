int	iKeyID

int iSwitchMode
int iSize
int iCount
float fEnergy
ref rArmor
ref rHat
ref rGlasses
ref rEnergyItem
string_var sTexture

begin Function {iKeyID}

	; Reset support timer
	AuxTimerStop "*SNV_KeySupport"

	; Toggle vision mode
	if eval (!MenuMode && Player.GetDead == 0 && AuxTimerTimeLeft "*SNV_KeyToggle" != -1)
	else
		return
	endif

	AuxTimerStop "*SNV_KeyToggle"
	rHat = Player.GetEqObj 10
	rGlasses = Player.GetEqObj 11
	if eval Goo1.AuxVarGetFlt "*SNV_INI" 3 && Player.HasPerk (EditorIDToFormID "CWxImplantVisionModePerk")
		iSwitchMode = 1
	elseif eval IsFormValid rHat && (IsPA rHat || HasKeyword rHat "ModernNightVision")
		rArmor = rHat
		iSwitchMode = 1
	elseif eval IsFormValid rGlasses && (IsPA rGlasses || HasKeyword rGlasses "ModernNightVision")
		rArmor = rGlasses
		iSwitchMode = 1
	endif

	if eval iSwitchMode == 0
		return
	endif

	; Check for energy requirements
	if eval Goo1.AuxVarGetFlt "*SNV_INI" 8 && Goo1.AuxVarGetFlt "SNV_Mode" 2 == 0
		iSize = Goo1.AuxVarSize "*SNV_Energy"
		while (iSize -= 2) > -1
			if eval (iCount = Player.GetItemCount (rEnergyItem = Goo1.AuxVarGetRef "*SNV_Energy" iSize)) > 0
				break
			endif
		loop
		SetUIStringAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\ItemName\string" "%z" (GetAmmoName 1 rEnergyItem)
		SetUIStringAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\CountText\string" "%g" iCount
		SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyCount" iCount
		SetUIFloatGradual "HUDMainMenu\ModernNightVision\EnergyDisplay\CountText\_Alpha" 510
		SetUIFloatGradual "HUDMainMenu\ModernNightVision\EnergyDisplay\CountText\_Alpha" 510 0 1
		if eval iCount > 0
			Player.RemoveItem rEnergyItem 1 1
			Player.AddItemAlt (GetAmmoCasing rEnergyItem) 1
			Goo1.AuxVarSetFlt "SNV_Mode" (fEnergy = Goo1.AuxVarGetFlt "*SNV_Energy" (iSize + 1)) 2
			Goo1.AuxVarSetFlt "SNV_Mode" fEnergy 3
			SetUIFloatGradual "HUDMainMenu\ModernNightVision\_EnergyCharge" 0 fEnergy 1
			SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyMax" fEnergy
			PlaySound WPNPistolLaserCharge
		else
			MessageExAlt 3 "#0|You have no ammo to charge your vision modes."
			PlaySound OBJSwitchButtonBFail
			SetUIFloatGradual "HUDMainMenu\ModernNightVision\_EnergyAlpha" 2 0 1
			return
		endif
	endif

	; Get state
	iSwitchMode = Goo1.AuxVarGetFlt "SNV_Mode" 0

	; Play animation
	if eval Goo1.AuxVarGetFlt "*SNV_INI" 5 && IsPC1stPerson
		if eval Player.IsIdlePlaying || Player.IsAnimPlayingEx 5 || Player.IsAnimPlayingEx 4 || Player.GetAnimAction == 13 || Player.GetIsRagdolled || IsInKillCam; || GetPlayerControlsDisabled 1 1 0 1 1 0 0
			return
		else
			if eval Player.IsAnimSequencePlaying "ModernNightVision\On.kf"
				Player.DeactivateAnim "ModernNightVision\On.kf"
			endif
			if eval Player.IsAnimSequencePlaying "ModernNightVision\Off.kf"
				Player.DeactivateAnim "ModernNightVision\Off.kf"
			endif
			if eval GSIV 0 > 0
				SetSmoothIronsightsValue 0 0
			endif
			DisablePlayerControlsAltEx 30100
			Goo1.AuxVarSetRef "SNV_Armor" rArmor
			Player.PlayAnimationPath "ModernNightVision\On.kf" 1
			AuxTimerStart "*SNV_Animation" 1.05 14
			Goo1.AuxVarSetFlt "*SNV_Animation" 1

			; Overlay fade
			if eval iSwitchMode == 0
				if eval Goo1.AuxVarGetFlt "*SNV_INI" 4
					SetUIFloatAlt "HUDMainMenu\ModernNightVision\_Mode" 0
				endif
				SetUIFloatGradual "HUDMainMenu\ModernNightVision\_AlphaOverlay" 0 1 0.2
				SetUIFloatGradual "HUDMainMenu\ModernNightVision\_EnergyAlpha" 0 1 0.2
			endif

		endif

	; Skip animation
	else
		Goo1.AuxVarSetRef "SNV_Armor" rArmor
		; Overlay fade
		if eval iSwitchMode == 0
			SetUIFloatGradual "HUDMainMenu\ModernNightVision\_AlphaOverlay" 1
			SetUIFloatGradual "HUDMainMenu\ModernNightVision\_EnergyAlpha" 0 1 0.2
		endif
		Call (CompileScript "ModernNightVision/ToggleOn.gek") 0
	endif

	; Update overlay
	if eval iSwitchMode == 0
		if eval HasKeyword rArmor "SNV_Override" == 0
			rArmor = Caps001
		endif
		sTexture = AuxVarGetStr "*SNV_Override" 12 rArmor
		SetUIStringAlt "HUDMainMenu\ModernNightVision\Overlay\_filename_0" ("Interface\Stentorious\ModernNightVision\Overlay\" + (sTexture) + ".dds")
		SetUIStringAlt "HUDMainMenu\ModernNightVision\Eyebox\_filename_0" ("Interface\Stentorious\ModernNightVision\Overlay\" + (sTexture) + "_eyebox.dds")
		sTexture = AuxVarGetStr "*SNV_Override" 13 rArmor
		SetUIStringAlt "HUDMainMenu\ModernNightVision\Overlay\_filename_1" ("Interface\Stentorious\ModernNightVision\Overlay\" + (sTexture) + ".dds")
		SetUIStringAlt "HUDMainMenu\ModernNightVision\Eyebox\_filename_1" ("Interface\Stentorious\ModernNightVision\Overlay\" + (sTexture) + "_eyebox.dds")
		sv_Destruct sTexture

		; Force trait assignment update
		ModUIFloat "HUDMainMenu\ModernNightVision\_Mode" 1
		ModUIFloat "HUDMainMenu\ModernNightVision\_Mode" -1

	endif
end