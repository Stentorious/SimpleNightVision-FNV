; Modern Night Vision - Stentorious

; Aux Vars
; "SNV_Mode"
; 0 = On/Off state
; 1 = Vision mode state
; 2 = Remaining energy

; "*SNV_INI"
; 0 = Static Strength (Night Vision)
; 1 = Static Strength (Thermal Vision)
; 2 = Pip-Boy overlay opacity mul
; 3 = Cyberware support
; 4 = Vision mode switch behavior
; 5 = First person anims
; 6 = Screen Blur (Night Vision)
; 7 = Screen Blur (Thermal Vision)
; 8 = Energy Cell power
; 9 = Current energy consumption rate
; 10 = Remaining charge text style
; 11 = Energy cell count text style

; "*SNV_Override"
; 0 = Color - Red (Night Vision)
; 1 = Color - Red (Night Vision)
; 2 = Color - Red (Night Vision)
; 3 = Color - Systemcolor (Night Vision)
; 4 = Overall brightness (Night Vision)
; 5 = Energy use multiplier (Night Vision)
; 6 = Color - Red (Thermal Vision)
; 7 = Color - Red (Thermal Vision)
; 8 = Color - Red (Thermal Vision)
; 9 = Color - Systemcolor (Thermal Vision)
; 10 = Overall brightness (Thermal Vision)
; 11 = Energy use multiplier (Thermal Vision)
; 12 = Overlay Texture (Night Vision)
; 13 = Overlay Texture (Thermal Vision)

Goo1.AuxVarSetFlt "*SNV_INI" 0 0
Goo1.AuxVarSetFlt "*SNV_INI" 0 1
Goo1.AuxVarSetFlt "*SNV_INI" 0 2
Goo1.AuxVarSetFlt "*SNV_INI" 0 3
Goo1.AuxVarSetFlt "*SNV_INI" 0 4
Goo1.AuxVarSetFlt "*SNV_INI" 0 5
Goo1.AuxVarSetFlt "*SNV_INI" 0 6
Goo1.AuxVarSetFlt "*SNV_INI" 0 7
Goo1.AuxVarSetFlt "*SNV_INI" 0 8
Goo1.AuxVarSetFlt "*SNV_INI" 0 9
Goo1.AuxVarSetFlt "*SNV_INI" 0 10
Goo1.AuxVarSetFlt "*SNV_INI" 0 11

; Check for requirements
if eval IsModLoaded "Enhanced Vision.esp" || IsModLoaded "Advanced Recon Tech.esm"
	MessageBoxEx "Conflicting Night Vision mod detected!%rModern Night Vision has been disabled."
	return
endif
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall xNVSE 6.3.10+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 360
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 165
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif
if GetPluginVersion "kNVSE" < 37
	MessageBoxEx "Modern Night Vision missing requirement!%rInstall latest kNVSE plugin."
	return
endif
if eval FileExists "UIO\Public\RadiationVisuals.txt" && FileExists "Textures\Interface\Stentorious\RadiationVisuals\0_0.dds" == 0
	MessageBoxEx "Modern Night Vision texture preload error.%rUpdate Radiation Visuals to latest version."
	return
endif
Goo1.AuxVarSetFlt "*SNV_Init" 1

float fINI = 0

string_var sINIPath = "Stentorious\ModernNightVision.ini"
if eval GetPluginVersion "NewVegasReloaded" >= 4
	sINIPath = "Stentorious\ModernNightVision_NVR.ini"
	Goo1.AuxVarSetFlt "*_NewVegasReloaded.dll" 1
endif

; Init static animation
SetUIFloatGradual "HUDMainMenu\ModernNightVision\Static\_index" 0 9.999 0.5 3

; Hazmat darklight cowl support
if eval GetObjectEffect NVDLC03GhostHoodlessHead == NVDLC03GhostHoodGogglesOE
	SetObjectEffect NVDLC03GhostHoodlessHead
	AssignKeyword NVDLC03GhostHoodlessHead "ModernNightVision"
endif

; Riot gear support
ref rHelmet = GetObjectEffect NVDLC04HelmetRiotGear
if rHelmet == NVDLC04HelmetRiotGearOE
	if GetNthEffectBase rHelmet 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 3
	endif
	if GetNthEffectBase rHelmet 2 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 2
	endif
	if GetNthEffectBase rHelmet 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 0
	endif
endif
AssignKeyword NVDLC04HelmetRiotGear "ModernNightVision"

; Advanced riot gear support
rHelmet = GetObjectEffect NVDLC04HelmetRiotGearCustom
if rHelmet == NVDLC04HelmetRiotGearCustomOE
	if GetNthEffectBase rHelmet 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 3
	endif
		if GetNthEffectBase rHelmet 2 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 2
	endif
	if GetNthEffectBase rHelmet 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 0
	endif
endif
AssignKeyword NVDLC04HelmetRiotGearCustom "ModernNightVision"

; Elite riot gear support
rHelmet = GetObjectEffect NVDLC04HelmetRiotGearUnique
if rHelmet == NVDLC04HelmetRiotGearUniqueOE
	if GetNthEffectBase rHelmet 4 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 4
	endif
		if GetNthEffectBase rHelmet 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 3
	endif
	if GetNthEffectBase rHelmet 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 0
	endif
endif
AssignKeyword NVDLC04HelmetRiotGearUnique "ModernNightVision"

; TTW Night vision goggles support
if eval IsModLoaded "TaleOfTwoWastelands.esm"
	rHelmet = EditorIDToFormID "TTWNightVisionGoggles"
	if eval IsScripted rHelmet
		RemoveScript rHelmet
		AssignKeyword rHelmet "ModernNightVision"
	endif
	rHelmet = EditorIDToFormID "TTWReinforcedNightVisionGoggles"
	if eval IsScripted rHelmet
		RemoveScript rHelmet
		AssignKeyword rHelmet "ModernNightVision"
	endif
endif

; Night vision
; Color
string_var sINI = GetINIString_Cached "NightVision:sColor" (sINIPath)
int iLength = Sv_Length sINI
if eval iLength == 6 && TestExpr (ToNumber sINI 1 1)
	AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[0:1]) 1) 0 Caps001
	AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[2:3]) 1) 1 Caps001
	AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[4:5]) 1) 2 Caps001
	AuxVarSetFlt "*SNV_Override" -1 3 Caps001
elseif eval iLength == 1 && TestExpr (ToNumber sINI 0 1)
	fINI = ToNumber sINI
	if eval fINI < 6 && fINI > -1
		AuxVarSetFlt "*SNV_Override" 255 0 Caps001
		AuxVarSetFlt "*SNV_Override" 255 1 Caps001
		AuxVarSetFlt "*SNV_Override" 255 2 Caps001
		AuxVarSetFlt "*SNV_Override" fINI 3 Caps001
	endif
endif

; Cinematic: Brightness (Add) Overall brightness
AuxVarSetFlt "*SNV_Override" (GetMaxOf 1 (GetINIFloat_Cached "NightVision:fOverallBrightness" (sINIPath))) 4 Caps001

; Energy use multiplier
AuxVarSetFlt "*SNV_Override" (GetMaxOf 0 (GetINIFloat_Cached "NightVision:fEnergyConsumption" (sINIPath))) 5 Caps001

; IMOD traits
; Default traits
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 9 1			; HDR: Emissive Mult (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 11 1			; HDR: Target LUM (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 13 1			; HDR: Upper LUM Clamp (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 15 1			; HDR: Bright Scale (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 25 1			; HDR: Sunlight Dimmer (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 37 1			; Cinematic: Saturation (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 39 1			; Cinematic: Contrast (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 43 1			; Cinematic: Brightness (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 55 0			; Radial Blur: Strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 56 0			; Radial Blur: Rampup
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 57 0			; Radial Blur: Up Start
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 50 255		; Cinematic: Tint: Alpha

; Mode specific
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 10 5			; HDR: Emissive Mult (Add) 			Emissive bloom strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 16 1			; HDR: Bright Scale (Add) 			General bloom strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 17 0.75		; HDR: Bright Clamp (Multiply) 			Prevents overblown bloom
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 19 1			; HDR: LUM Ramp No Tex (Multiply) 	Sky Brightness
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 39 0.1;25		; Cinematic: Contrast (Multiply) 	Decrease color contrast
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 42 1			; Cinematic: Contrast Avg Lum (Add) Increase brightness contrast

; Configurable traits
fINI = GetMaxOf 0 (GetINIFloat_Cached "NightVision:fMoonlightBrightness" (sINIPath))
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 25 fINI				; HDR: Sunlight Dimmer (Multiply) 	Skylight brightness
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 27 (fINI * 1.75)		; HDR: Grass Dimmer (Multiply) 		Grass brightness
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 63 ((GetMaxOf 0 (GetMinOf 2 (GetINIFloat_Cached "NightVision:fMotionBlur" (sINIPath)))) * 2)			; Full-Screen Motion Blur: Strength
;Goo1.AuxVarSetFlt "*SNV_INI" (INI = GetMaxOf 0 (GetMinOf 2 (GetINIFloat_Cached "NightVision:fScreenBlur" (sINIPath)))) 6
;SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 45 INI		; Blur: Blur Radius (Screen Blur)

; Depth of field
if eval (fINI = ((GetMaxOf 0 (GetMinOf 10 (GetINIFloat_Cached "NightVision:fDepthBlur" (sINIPath)))) * 69.99104)) > 0
	SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 60 1				; Depth of Field: Strength
	SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 67 1				; Depth of Field: Mode (Front)
	SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 61 fINI			; Depth of Field: Distance
	SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 62 (fINI * 0.75)	; Depth of Field: Range
endif

; New Vegas Reloaded support
;if eval Goo1.AuxVarGetFlt "*_NewVegasReloaded.dll"
;endif

Goo1.AuxVarSetFlt "*SNV_INI" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat_Cached "NightVision:fStaticStrength" (sINIPath)))) * 255) 0
SetUIFloatAlt "HudMainMenu\ModernNightVision\Overlay\_Alpha_0" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat_Cached "NightVision:fHUDOverlayStrength" (sINIPath)))) * 255)
SetUIFloatAlt "HudMainMenu\ModernNightVision\Overlay\_Scale_0" ((GetMaxOf 1 (GetMinOf 1.5 (GetINIFloat_Cached "NightVision:fHUDOverlayScale" (sINIPath)))) * 1.15)

; Thermal vision
; Set color
sINI = GetINIString_Cached "ThermalVision:sColor" (sINIPath)
iLength = Sv_Length sINI
if eval iLength == 6 && TestExpr (ToNumber sINI 1 1)
	AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[0:1]) 1) 6 Caps001
	AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[2:3]) 1) 7 Caps001
	AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[4:5]) 1) 8 Caps001
	AuxVarSetFlt "*SNV_Override" -1 9 Caps001
elseif eval iLength == 1 && TestExpr (ToNumber sINI 0 1)
	fINI = ToNumber sINI
	if eval fINI < 6 && fINI > -1
		AuxVarSetFlt "*SNV_Override" 255 6 Caps001
		AuxVarSetFlt "*SNV_Override" 255 7 Caps001
		AuxVarSetFlt "*SNV_Override" 255 8 Caps001
		AuxVarSetFlt "*SNV_Override" fINI 9 Caps001
	endif
endif

; Cinematic: Brightness (Add) Overall brightness
AuxVarSetFlt "*SNV_Override" (GetMaxOf 1 (GetINIFloat_Cached "ThermalVision:fOverallBrightness" (sINIPath))) 10 Caps001

; Energy use multiplier
AuxVarSetFlt "*SNV_Override" (GetMaxOf 0 (GetINIFloat_Cached "ThermalVision:fEnergyConsumption" (sINIPath))) 11 Caps001

; Overlay textures
AuxVarSetStr "*SNV_Override" (GetINIString_Cached "NightVision:sOverlayTexture" (sINIPath) "night_vision") 12 Caps001
AuxVarSetStr "*SNV_Override" (GetINIString_Cached "ThermalVision:sOverlayTexture" (sINIPath) "thermal_vision") 13 Caps001

; IMOD traits
; Default traits
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 9 1				; HDR: Emissive Mult (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 11 1			; HDR: Target LUM (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 13 1			; HDR: Upper LUM Clamp (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 15 1			; HDR: Bright Scale (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 25 1			; HDR: Sunlight Dimmer (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 37 1			; Cinematic: Saturation (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 39 1			; Cinematic: Contrast (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 43 1			; Cinematic: Brightness (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 55 0			; Radial Blur: Strength
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 56 0			; Radial Blur: Rampup
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 57 0			; Radial Blur: Up Start
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 50 255			; Cinematic: Tint: Alpha

; Mode specific
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 10 5			; HDR: Emissive Mult (Add) 			Emissive bloom strength
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 16 1			; HDR: Bright Scale (Add) 			General bloom strength
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 17 1.0			; HDR: Bright Clamp (Multiply) 		Prevents overblown bloom
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 19 0.75			; HDR: LUM Ramp No Tex (Multiply) 	Sky Brightness
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 39 0.125		; Cinematic: Contrast (Multiply) 	Decrease color contrast
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 42 1			; Cinematic: Contrast Avg Lum (Add) Increase brightness contrast

; Configurable traits
fINI = (GetMaxOf 0 (GetINIFloat_Cached "ThermalVision:fMoonlightBrightness" (sINIPath)))
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 25 fINI						; HDR: Sunlight Dimmer (Multiply) 	Skylight brightness
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 27 0						; HDR: Grass Dimmer (Multiply) 		Grass brightness
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 28 (GetMaxOf 2.5 (fINI * 1.75)); HDR: Grass Dimmer (Add) 			Grass brightness
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 63 ((GetMaxOf 0 (GetMinOf 2 (GetINIFloat_Cached "ThermalVision:fMotionBlur" (sINIPath)))) * 2)		; Full-Screen Motion Blur: Strength
;Goo1.AuxVarSetFlt "*SNV_INI" (INI = GetMaxOf 0 (GetMinOf 2 (GetINIFloat_Cached "ThermalVision:fScreenBlur" (sINIPath)))) 6
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 45 INI		; Blur: Blur Radius (Screen Blur)

; Depth of field
if eval (fINI = ((GetMaxOf 0 (GetMinOf 10 (GetINIFloat_Cached "ThermalVision:fDepthBlur" (sINIPath)))) * 69.99104)) > 0
	SetImageSpaceModTrait NVDLC04RedNightvisionISFX 60 1				; Depth of Field: Strength
	SetImageSpaceModTrait NVDLC04RedNightvisionISFX 67 1				; Depth of Field: Mode (Front)
	SetImageSpaceModTrait NVDLC04RedNightvisionISFX 61 fINI				; Depth of Field: Distance
	SetImageSpaceModTrait NVDLC04RedNightvisionISFX 62 (fINI * 0.75)	; Depth of Field: Range
endif

; New Vegas Reloaded support
;if eval Goo1.AuxVarGetFlt "*_NewVegasReloaded.dll"
;endif

Goo1.AuxVarSetFlt "*SNV_INI" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat_Cached "ThermalVision:fStaticStrength" (sINIPath)))) * 255 * 1.1) 1
SetUIFloatAlt "HudMainMenu\ModernNightVision\Overlay\_Alpha_1" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat_Cached "ThermalVision:fHUDOverlayStrength" (sINIPath)))) * 255)
SetUIFloatAlt "HudMainMenu\ModernNightVision\Overlay\_Scale_1" ((GetMaxOf 1 (GetMinOf 1.5 (GetINIFloat_Cached "ThermalVision:fHUDOverlayScale" (sINIPath)))) * 1.15)

; Thermal vision shader traits
SetEffectShaderTraitNumeric effectAbsorb 0 24					; Flags
SetEffectShaderTraitNumeric effectAbsorb 4 16777215				; Fill/Texture Effect - Color (RGB)
SetEffectShaderTraitNumeric effectAbsorb 8 1					; Fill/Texture Effect - Presistent Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 10 0					; Fill/Texture Effect - Alpha Pulse Frequency
SetEffectShaderTraitNumeric effectAbsorb 21 1					; Fill/Texture Effect - Full Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 13 1					; Edge Effect - Fall Off
SetEffectShaderTraitNumeric effectAbsorb 18 1					; Fill/Texture Effect - Presistent Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 20 0					; Edge Effect - Alpha Pulse Frequency
SetEffectShaderTraitNumeric effectAbsorb 22 1					; Edge Effect - Full Alpha Ratio

; No edge effect, more consistent
SetEffectShaderTraitNumeric effectAbsorb 1 10					; Membrane Shader - Source Blend Mode
SetEffectShaderTraitNumeric effectAbsorb 23 2					; Membrane Shader - Dest Blend Mode
SetEffectShaderTraitNumeric effectAbsorb 14 16777215			; Edge Effect - Color (RGB)

; With edge effect, better looking, edge effect can bug out and disappear
;SetEffectShaderTraitNumeric effectAbsorb 1 10
;SetEffectShaderTraitNumeric effectAbsorb 23 2
;SetEffectShaderTraitNumeric effectAbsorb 14 3355443

Goo1.AuxVarSetFlt "*SNV_INI" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat_Cached "General:fPipBoyStrength" (sINIPath)))) 2

;Set event handlers
fINI = GetINIFloat_Cached "General:iKeybind" (sINIPath)
SetOnKeyDownEventHandler (CompileScript "ModernNightVision\OnKeyDown.gek") 1 fINI
SetOnKeyUpEventHandler (CompileScript "ModernNightVision\OnKeyUp.gek") 1 fINI
SetEventHandler "OnCellEnter" (CompileScript "ModernNightVision\OnCellEnter.gek")
SetOnMenuOpenEventHandler (CompileScript "ModernNightVision\OnMenuOpen.gek") 1 1002
SetOnMenuOpenEventHandler (CompileScript "ModernNightVision\OnMenuOpen.gek") 1 1003
SetOnMenuOpenEventHandler (CompileScript "ModernNightVision\OnMenuOpen.gek") 1 1023
SetOnMenuCloseEventHandler (CompileScript "ModernNightVision\OnMenuClose.gek") 1 1002
SetOnMenuCloseEventHandler (CompileScript "ModernNightVision\OnMenuClose.gek") 1 1003
SetOnMenuCloseEventHandler (CompileScript "ModernNightVision\OnMenuClose.gek") 1 1023

; Controller support
if eval GetINIFloat_Cached "General:bControllerSupport" (sINIPath)
	SetEventHandler "OnControlDown:27" (begin function {int iKeyCode}
		if eval GetController
			DisableButton 1
		endif
	end)
	SetEventHandler "OnControlUp:27" (begin function {int iKeyCode}
		if eval GetController
			EnableButton 1
		endif
	end)
	SetEventHandler "OnButtonDown:1" (begin function {int iKeyCode}
		if eval GetController && IsControlPressed 27
			Call (CompileScript "ModernNightVision\OnKeyDown.gek")
		endif
	end)
	SetEventHandler "OnButtonUp:1" (begin function {int iKeyCode}
		if eval GetController
			Call (CompileScript "ModernNightVision\OnKeyUp.gek")
		endif
	end)
endif

; Cyberware support
if eval IsModLoaded "Cyberware.esm" && GetINIFloat_Cached "General:bOverrideCyberware" (sINIPath) == 1
	SetOnRemovePerkEventHandler 1 (CompileScript "ModernNightVision\OnRemovePerkCyb.gek") 0 (EditorIDToFormID "CWxImplantVisionModePerk")
	SetOnQuestStageEventHandler (CompileScript "ModernNightVision\OnQuestStageCyb.gek") 1 (EditorIDToFormID "CWxBionicEyesQuest") 1 1
	Goo1.AuxVarSetFlt "*SNV_INI" 1 3
endif

Goo1.AuxVarSetFlt "*SNV_INI" (eval GetINIFloat_Cached "General:iSwitchMode" (sINIPath)) 4								; Vision mode control
Goo1.AuxVarSetFlt "*SNV_INI" (eval GetINIFloat_Cached "General:b1stPersonAnimations" (sINIPath)) 5						; Animation support

; Energy consumption
Goo1.AuxVarSetFlt "*SNV_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:fEnergyCellPower" (sINIPath))) 8
if eval (fINI = Goo1.AuxVarGetFlt "*SNV_INI" 8) == 0
	AuxVarSetFlt "*SNV_Override" 0 5 Caps001
	AuxVarSetFlt "*SNV_Override" 0 11 Caps001
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyCharge" 1
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyMax" 1
	SetUIStringAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\ChargeText\string" "100%%"
else
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyMax" fINI
endif

; Energy display UI
Goo1.AuxVarSetFlt "*SNV_INI" (GetMaxOf 0 (GetMinOf 2 (GetINIFloat_Cached "EnergyDisplay:iChargeStyle" (sINIPath)))) 10	; Text style
if eval Goo1.AuxVarGetFlt "*SNV_INI" 10 == 0
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_ChargeText" 0
endif
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_IconScale" (GetINIFloat_Cached "EnergyDisplay:fScale" (sINIPath))
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_OffsetX" (GetINIFloat_Cached "EnergyDisplay:fOffsetX" (sINIPath))
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_OffsetY" (GetINIFloat_Cached "EnergyDisplay:fOffsetY" (sINIPath))
SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_IconStyle" (eval GetINIFloat_Cached "EnergyDisplay:iIconStyle" (sINIPath))
if eval IsModLoaded "DarNifiedUINV.esp"
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\ChargeText\font" 6
	ModUIFloat "HUDMainMenu\ModernNightVision\EnergyDisplay\ChargeText\x" (3 * (GetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_IconScale"))
endif

; Overlay inertia
SetUIFloatAlt "HUDMainMenu\ModernNightVision\_Inertia" (GetINIFloat_Cached "General:fOverlayInertia" (sINIPath))

; Config override support
array_var aOverrideINIs = GetFilesInFolder "Config\Stentorious\ModernNightVision" "*.ini"
int iSize = 0
if eval (iSize = Ar_Size aOverrideINIs) > 0
	while (iSize -= 1) > -1
		sINI = aOverrideINIs[iSize]
		sINI = sINI[0:-5]
		if eval IsBaseForm (rHelmet = EditorIDToFormID (sINI)) == 0
			continue
		endif

		; Override keyword
		AssignKeyword rHelmet "SNV_Override"
		AssignKeyword rHelmet "ModernNightVision"

		; Night vision
		; Color
		sINI = GetINIString_Cached "NightVision:sColor" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (GetINIString_Cached "NightVision:sColor" (sINIPath))
		iLength = Sv_Length sINI
		if eval iLength == 6 && TestExpr (ToNumber sINI 1 1)
			AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[0:1]) 1) 0 rHelmet
			AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[2:3]) 1) 1 rHelmet
			AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[4:5]) 1) 2 rHelmet
			AuxVarSetFlt "*SNV_Override" -1 3 rHelmet
		elseif eval iLength == 1 && TestExpr (ToNumber sINI 0 1)
			fINI = ToNumber sINI
			if eval fINI < 6 && fINI > -1
				AuxVarSetFlt "*SNV_Override" 255 0 rHelmet
				AuxVarSetFlt "*SNV_Override" 255 1 rHelmet
				AuxVarSetFlt "*SNV_Override" 255 2 rHelmet
				AuxVarSetFlt "*SNV_Override" fINI 3 rHelmet
			endif
		endif

		; Brightness
		AuxVarSetFlt "*SNV_Override" (GetMaxOf 1 (GetINIFloat_Cached "NightVision:fOverallBrightness" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (AuxVarGetFlt "*SNV_Override" 4 Caps001))) 4 rHelmet

		; Energy consumption
		AuxVarSetFlt "*SNV_Override" (GetMaxOf 0 (GetINIFloat_Cached "NightVision:fEnergyConsumption" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (AuxVarGetFlt "*SNV_Override" 5 Caps001))) 5 rHelmet

		; Thermal vision
		; Color
		sINI = GetINIString_Cached "ThermalVision:sColor" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (GetINIString_Cached "ThermalVision:sColor" (sINIPath))
		iLength = Sv_Length sINI
		if eval iLength == 6 && TestExpr (ToNumber sINI 1 1)
			AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[0:1]) 1) 6 rHelmet
			AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[2:3]) 1) 7 rHelmet
			AuxVarSetFlt "*SNV_Override" (ToNumber (sINI[4:5]) 1) 8 rHelmet
			AuxVarSetFlt "*SNV_Override" -1 9 rHelmet
		elseif eval iLength == 1 && TestExpr (ToNumber sINI 0 1)
			fINI = ToNumber sINI
			if eval fINI < 6 && fINI > -1
				AuxVarSetFlt "*SNV_Override" 255 6 rHelmet
				AuxVarSetFlt "*SNV_Override" 255 7 rHelmet
				AuxVarSetFlt "*SNV_Override" 255 8 rHelmet
				AuxVarSetFlt "*SNV_Override" fINI 9 rHelmet
			endif
		endif

		; Thermal vision brightness
		AuxVarSetFlt "*SNV_Override" (GetMaxOf 1 (GetINIFloat_Cached "ThermalVision:fOverallBrightness" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (AuxVarGetFlt "*SNV_Override" 10 Caps001))) 10 rHelmet

		; Energy consumption
		AuxVarSetFlt "*SNV_Override" (GetMaxOf 0 (GetINIFloat_Cached "ThermalVision:fEnergyConsumption" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (AuxVarGetFlt "*SNV_Override" 11 Caps001))) 11 rHelmet

		; Overlay textures
		AuxVarSetStr "*SNV_Override" (GetINIString_Cached "NightVision:sOverlayTexture" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (AuxVarGetStr "*SNV_Override" 12 Caps001)) 12 rHelmet
		AuxVarSetStr "*SNV_Override" (GetINIString_Cached "ThermalVision:sOverlayTexture" ("Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) (AuxVarGetStr "*SNV_Override" 13 Caps001)) 13 rHelmet

		CloseFileSO ("Config\Stentorious\ModernNightVision\" + (aOverrideINIs[iSize])) 0

	loop
endif

CloseFileSO (sINIPath) 0
aOverrideINIs = Ar_Null
Sv_Destruct sINI sINIPath
