ref rOwnerForm
string_var sTimerName

ref rHat
ref rGlasses
array_var aINISections

begin Function {sTimerName, rOwnerForm}

	; Get headgear
	rHat = Player.GetEqObj 10
	rGlasses = Player.GetEqObj 11

	; Display message box if valid headgear can support vision modes
	if eval IsFormValid rHat && IsPA rHat == 0 && HasKeyword rHat "SimpleNightVision" == 0
		MessageBoxExAlt (begin function {int iButton}
			if iButton == 0
				aINISections = GetINISection "SimpleNightVision" "..\KEYWORDS\SimpleNightVision.ini" 1
				SetINIString ("SimpleNightVision:" + $(Ar_Size aINISections)) (GetEditorID rHat) "..\KEYWORDS\SimpleNightVision.ini"
				AssignKeyword rHat "SimpleNightVision"
			endif
		end) "^Simple Night Vision^Add support to %n?|Ok|Cancel" rHat
	elseif eval IsFormValid rGlasses && IsPA rGlasses == 0 && HasKeyword rGlasses "SimpleNightVision" == 0
		MessageBoxExAlt (begin function {int iButton}
			if iButton == 0
				aINISections = GetINISection "SimpleNightVision" "..\KEYWORDS\SimpleNightVision.ini" 1
				SetINIString ("SimpleNightVision:" + $(Ar_Size aINISections)) (GetEditorID rGlasses) "..\KEYWORDS\SimpleNightVision.ini"
				AssignKeyword rGlasses "SimpleNightVision"
			endif
		end) "^Simple Night Vision^Add support to %n?|Ok|Cancel" rGlasses
	endif

	aINISections = Ar_Null

end