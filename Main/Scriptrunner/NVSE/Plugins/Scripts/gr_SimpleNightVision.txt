; Simple Night Vision 2.6 - Stentorious

; Check for requirements
if eval IsModLoaded "Enhanced Vision.esp" || IsModLoaded "Advanced Recon Tech.esm"	
	MessageBoxEx "Conflicting Night Vision mod detected!%rSimple Night Vision has been disabled."
	return
endif
if GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 2
else
	MessageBoxEx "Simple Night Vision missing requirement!%rInstall xNVSE 6.3.3+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 360
	MessageBoxEx "Simple Night Vision missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 165
	MessageBoxEx "Simple Night Vision missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif

SetUIFloatGradual "HudMainMenu\_SNV+0" 0 9.999 0.5 3

; Add OWB Hazmat Darklight Cowl support
if eval GetObjectEffect NVDLC03GhostHoodlessHead == NVDLC03GhostHoodGogglesOE
	SetObjectEffect NVDLC03GhostHoodlessHead
	AssignKeyword NVDLC03GhostHoodlessHead "SimpleNightVision"
endif

; Add LR Riot Gear support
ref stR1 = GetObjectEffect NVDLC04HelmetRiotGear
if stR1 == NVDLC04HelmetRiotGearOE
	if GetNthEffectBase stR1 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 0
	endif
	if GetNthEffectBase stR1 2 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 2
	endif
	if GetNthEffectBase stR1 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearOE 3
	endif
endif
AssignKeyword NVDLC04HelmetRiotGear "SimpleNightVision"

stR1 = GetObjectEffect NVDLC04HelmetRiotGearCustom
if stR1 == NVDLC04HelmetRiotGearCustomOE
	if GetNthEffectBase stR1 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 0
	endif
	if GetNthEffectBase stR1 2 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 2
	endif
	if GetNthEffectBase stR1 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearCustomOE 3
	endif
endif
AssignKeyword NVDLC04HelmetRiotGearCustom "SimpleNightVision"

stR1 = GetObjectEffect NVDLC04HelmetRiotGearUnique
if stR1 == NVDLC04HelmetRiotGearUniqueOE
	if GetNthEffectBase stR1 0 == NVDLC04RedNightvisionBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 0
	endif
	if GetNthEffectBase stR1 3 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 3
	endif
	if GetNthEffectBase stR1 4 == NVDLC04DummySneakSightBE
		RemoveNthEffect NVDLC04HelmetRiotGearUniqueOE 4
	endif
endif
AssignKeyword NVDLC04HelmetRiotGearUnique "SimpleNightVision"

; Add TTW Night Vision Goggles support
if eval IsModLoaded "TaleOfTwoWastelands.esm"
	stR1 = EditorIDToFormID "TTWNightVisionGoggles"
	if eval IsScripted stR1
		RemoveScript stR1
		AssignKeyword stR1 "SimpleNightVision"
	endif
	stR1 = EditorIDToFormID "TTWReinforcedNightVisionGoggles"
	if eval IsScripted stR1
		RemoveScript stR1
		AssignKeyword stR1 "SimpleNightVision"
	endif
endif

; Set night vision IMOD traits
; Set default traits
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 9 1			; HDR: Emissive Mult (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 15 1			; HDR: Bright Scale (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 43 1			; Cinematic: Brightness (Multiply)
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 55 0			; Radial Blur: Strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 56 0			; Radial Blur: Rampup
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 57 0			; Radial Blur: Up Start

SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 10 5			; HDR: Emissive Mult (Add) 			Emissive bloom strength
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 18 1			; HDR: Bright Clamp (Add) 			Prevents overblown bloom
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 20 -0.5		; HDR: LUM Ramp No Tex (Add) 		Sky Brightness
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 39 0.125		; Cinematic: Contrast (Multiply) 	Decrease color contrast
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 42 1			; Cinematic: Contrast Avg Lum (Add) Increase brightness contrast
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 45 0.3		; Blur: Blur Radius 				Screen Blur
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 50 255		; Cinematic: Tint: Alpha

; Set configurable traits
float stF1 = (GetMaxOf 0 (GetMinOf 15 (GetINIFloat "NightVision:fMoonlightBrightness" "SimpleNightVision.ini")))
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 26 stF1		; HDR: Sunlight Dimmer (Add) 		Skylight brightness
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 28 stF1		; HDR: Grass Dimmer (Add) 			Grass brightness
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 44 (GetMaxOf 0 (GetMinOf 15 (GetINIFloat "NightVision:fOverallBrightness" "SimpleNightVision.ini")))		; Cinematic: Brightness (Add) Overall brightness
stF1 = (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "NightVision:fTintRed" "SimpleNightVision.ini")))
Goo1.AuxVarSetFlt "*SNV_INI" stF1 0
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 47 stF1		; Cinematic: Tint: Red
stF1 = (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "NightVision:fTintGreen" "SimpleNightVision.ini")))		
Goo1.AuxVarSetFlt "*SNV_INI" stF1 1
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 48 stF1		; Cinematic: Tint: Green
stF1 = (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "NightVision:fTintBlue" "SimpleNightVision.ini")))			
Goo1.AuxVarSetFlt "*SNV_INI" stF1 2
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 49 stF1		; Cinematic: Tint: Blue
SetImageSpaceModTrait NVDLC03GhostNightvisionISFX 63 ((GetMaxOf 0 (GetMinOf 2 (GetINIFloat "NightVision:fMotionBlur" "SimpleNightVision.ini"))) * 2)		; Full-Screen Motion Blur: Strength
Goo1.AuxVarSetFlt "*SNV_INI" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat "NightVision:fStaticStrength" "SimpleNightVision.ini"))) * 255) 3
SetUIFloatAlt "HudMainMenu\SimpleNightVision\Overlay\_Alpha_1" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat "NightVision:fHUDOverlayStrength" "SimpleNightVision.ini"))) * 255)

; Set thermal vision IMOD traits
; Set default traits
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 9 1				; HDR: Emissive Mult (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 11 1			; HDR: Target LUM (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 13 1			; HDR: Upper LUM Clamp (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 15 1			; HDR: Bright Scale (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 25 1			; HDR: Sunlight Dimmer (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 37 1			; Cinematic: Saturation (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 39 1			; Cinematic: Contrast (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 43 1			; Cinematic: Brightness (Multiply)
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 55 0			; Radial Blur: Strength
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 56 0			; Radial Blur: Rampup
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 57 0			; Radial Blur: Up Start

;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 10 5			; HDR: Emissive Mult (Add) 			Emissive bloom strength
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 18 1			; HDR: Bright Clamp (Add) 			Prevents overblown bloom
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 20 -0.5		; HDR: LUM Ramp No Tex (Add) 		Sky Brightness
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 39 0.125		; Cinematic: Contrast (Multiply) 	Decrease color contrast
;SetImageSpaceModTrait NVDLC04RedNightvisionISFX 42 1			; Cinematic: Contrast Avg Lum (Add) Increase brightness contrast
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 16 1			; HDR: Bright Scale (Add) 			General bloom strength
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 45 0.3			; Blur: Blur Radius 				Screen Blur
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 50 255			; Cinematic: Tint: Alpha

; Set configurable traits
stF1 = (GetMaxOf 0 (GetMinOf 15 (GetINIFloat "ThermalVision:fMoonlightBrightness" "SimpleNightVision.ini")))
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 26 stF1			; HDR: Sunlight Dimmer (Add) 		Skylight brightness
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 28 stF1			; HDR: Grass Dimmer (Add) 			Grass brightness
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 44 (GetMaxOf 0 (GetMinOf 15 (GetINIFloat "ThermalVision:fOverallBrightness" "SimpleNightVision.ini")))		; Cinematic: Brightness (Add) Overall brightness
stF1 = (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "ThermalVision:fTintRed" "SimpleNightVision.ini")))
Goo1.AuxVarSetFlt "*SNV_INI" stF1 4
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 47 stF1			; Cinematic: Tint: Red
stF1 = (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "ThermalVision:fTintGreen" "SimpleNightVision.ini")))		
Goo1.AuxVarSetFlt "*SNV_INI" stF1 5
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 48 stF1			; Cinematic: Tint: Green
stF1 = (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "ThermalVision:fTintBlue" "SimpleNightVision.ini")))			
Goo1.AuxVarSetFlt "*SNV_INI" stF1 6
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 49 stF1			; Cinematic: Tint: Blue
SetImageSpaceModTrait NVDLC04RedNightvisionISFX 63 ((GetMaxOf 0 (GetMinOf 2 (GetINIFloat "ThermalVision:fMotionBlur" "SimpleNightVision.ini"))) * 2)		; Full-Screen Motion Blur: Strength
Goo1.AuxVarSetFlt "*SNV_INI" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat "ThermalVision:fStaticStrength" "SimpleNightVision.ini"))) * 255) 7
SetUIFloatAlt "HudMainMenu\SimpleNightVision\Overlay\_Alpha_2" ((GetMaxOf 0 (GetMinOf 1 (GetINIFloat "ThermalVision:fHUDOverlayStrength" "SimpleNightVision.ini"))) * 255)

; Set thermal vision effect shader traits
SetEffectShaderTraitNumeric effectAbsorb 0 24					; Flags
SetEffectShaderTraitNumeric effectAbsorb 4 16777215				; Fill/Texture Effect - Color (RGB)
SetEffectShaderTraitNumeric effectAbsorb 8 1					; Fill/Texture Effect - Presistent Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 10 0					; Fill/Texture Effect - Alpha Pulse Frequency
SetEffectShaderTraitNumeric effectAbsorb 21 1					; Fill/Texture Effect - Full Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 13 1					; Edge Effect - Fall Off
SetEffectShaderTraitNumeric effectAbsorb 14 16777215			; Edge Effect - Color (RGB)
SetEffectShaderTraitNumeric effectAbsorb 20 0					; Edge Effect - Alpha Pulse Frequency
SetEffectShaderTraitNumeric effectAbsorb 22 1					; Edge Effect - Full Alpha Ratio
SetEffectShaderTraitNumeric effectAbsorb 23 2					; Membrane Shader - Dest Blend Mode

;Set Event Handlers
stF1 = GetINIFloat "General:iKeybind" "SimpleNightVision.ini"

SetOnKeyDownEventHandler (CompileScript "SimpleNightVision/OnKeyDown.gek") 1 stF1
SetEventHandler "OnCellEnter" (CompileScript "SimpleNightVision/OnCellEnter.gek")

if IsModLoaded "Cyberware.esm" && GetINIFloat "General:bOverrideCyberware" "SimpleNightVision.ini" == 1
	SetOnKeyUpEventHandler (CompileScript "SimpleNightVision/OnKeyUpCyb.gek") 1 stF1 
	SetOnRemovePerkEventHandler 1 (CompileScript "SimpleNightVision/OnRemovePerkCyb.gek") 0 (EditorIDToFormID "CWxImplantVisionModePerk") 
	SetOnQuestStageEventHandler (CompileScript "SimpleNightVision/OnQuestStageCyb.gek") 1 (EditorIDToFormID "CWxBionicEyesQuest") 1 1 
else
	SetOnKeyUpEventHandler (CompileScript "SimpleNightVision/OnKeyUp.gek") 1 stF1 
endif

PrintC "Simple Night Vision 2.6 loaded"
