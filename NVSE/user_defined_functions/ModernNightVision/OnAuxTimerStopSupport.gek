ref rOwner
string_var sTimerName

int iFlag
ref rArmor

begin Function {sTimerName, rOwner}

	; Hat
	if eval IsFormValid (rArmor = Player.GetEqObj 10)
		if eval IsPA rArmor || HasKeyword rArmor "ModernNightVision"
			iFlag = 2
		else
			iFlag = 1
		endif

	; Glasses
	elseif eval IsFormValid (rArmor = Player.GetEqObj 11)
		if eval IsPA rArmor || HasKeyword rArmor "ModernNightVision"
			iFlag = 2
		else
			iFlag = 1
		endif
	endif

	; Display message box
	if eval iFlag == 1
		Goo1.AuxVarSetRef "*SNV_Ref" rArmor
		MessageBoxExAlt (begin function {int iButton}
			if eval iButton != 0
				return
			endif
			ref rArmor = Goo1.AuxVarGetRef "*SNV_Ref"
			SetINIString ("ModernNightVision:" + $(Ar_Size (GetINISection "ModernNightVision" "..\KEYWORDS\ModernNightVision.ini" 1))) (GetEditorID rArmor) "..\KEYWORDS\ModernNightVision.ini"
			AssignKeyword rArmor "ModernNightVision"
			Call (CompileScript "ModernNightVision\UpdateEffect.gek") rArmor
			Goo1.AuxVarErase "*SNV_Ref"
		end) "^Modern Night Vision^Add support to %n?|Ok|Cancel" rArmor

	; Error message
	elseif eval iFlag == 2
		MessageExAlt 3 "#3|This armor already has vision mode support."
	else
		MessageExAlt 3 "#3|No valid armor for vision mode support."
	endif

	sv_Destruct sTimerName

end