int iIndex
int iVisionMode
float fEnergy
float fEnergyUse
ref rActor
string_var sEnergy

begin Function {iIndex}

	Goo1.AuxVarSetFlt "*SNV_Animation" 0

	; Status: Off
	if eval Goo1.AuxVarGetFlt "SNV_Mode" 0 == 0
		Goo1.AuxVarSetFlt "SNV_Mode" 1 0

		; Set overlay
		iVisionMode = Goo1.AuxVarGetFlt "SNV_Mode" 1

		; Energy
		AuxTimerStart "*SNV_Energy" 1 30

	; Status: On
	else
		; Check if thermal vision active
		if eval Goo1.AuxVarGetFlt "SNV_Mode" 1 == 1

			; Toggle off (Wrap mode)
			if eval Goo1.AuxVarGetFlt "*SNV_INI" 4
				Call (CompileScript "ModernNightVision/ToggleOff.gek") 1
				return

			; Enable night vision (Toggle mode)
			else

				; Remove thermal effect shader from all refs
				AuxTimerStop "*SNV_Thermal"
				if eval (iIndex = Goo1.AuxVarSize "*SNV_Shader") > 0
					while (iIndex -= 1) > -1
						if eval IsReference (rActor = Goo1.AuxVarGetRef "*SNV_Shader" iIndex)
							rActor.SMS effectAbsorb
						endif
					loop
				endif
			endif

		; Enable thermal vision
		else
			; Toggle off (Wrap mode) if thermal vision disabled
			if eval Goo1.AuxVarGetFlt "*SNV_INI" 4 && Goo1.AuxVarGetFlt "*SNV_INI" 7 == 0
				Call (CompileScript "ModernNightVision/ToggleOff.gek") 1
				return
			endif

			iVisionMode = 1
		endif
	endif

	if eval iVisionMode == 0
		IMOD NVDLC03GhostNightvisionISFX
		RIMOD NVDLC04RedNightvisionISFX
	elseif eval iVisionMode == 1
		IMOD NVDLC04RedNightvisionISFX
		RIMOD NVDLC03GhostNightvisionISFX
		AuxTimerStart "*SNV_Thermal" 5 30
	endif

	PlaySound UIPipBoyLightOn
	SetUIFloatAlt "HUDMainMenu\ModernNightVision\_Mode" iVisionMode
	SetUIFloatGradual "HUDMainMenu\ModernNightVision\_AlphaStatic" 0.8
	SetUIFloatGradual "HUDMainMenu\ModernNightVision\_AlphaStatic" 0.8 (GetUIFloatAlt "HUDMainMenu\ModernNightVision\_Static") 0.5
	Goo1.AuxVarSetFlt "SNV_Mode" iVisionMode 1

	; IMOD Transition
	Goo1.AuxVarSetFlt "*SNV_DoF" 1 0
	Goo1.AuxVarSetFlt "*SNV_DoF" 0 1

	; Update Energy Display text
	if eval GetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyRequired" == 1
		fEnergy = Goo1.AuxVarGetFlt "SNV_Mode" 2
		if eval GetUIFloatAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\_ChargeStyle" == 1
			sEnergy = sv_Construct "%g%%" (Flr (((fEnergy / (Goo1.AuxVarGetFlt "SNV_Mode" 3)) * 100) + 0.5))
		elseif eval (fEnergyUse = GetUIFloatAlt "HUDMainMenu\ModernNightVision\_EnergyUseCalc") > 0
			sEnergy = sv_Construct "%gs" (Flr ((fEnergy / fEnergyUse) + 0.5))
		else
			sEnergy = "-"
		endif
		SetUIStringAlt "HUDMainMenu\ModernNightVision\EnergyDisplay\ChargeText\string" "%z" sEnergy
	endif

	sv_Destruct sEnergy

end