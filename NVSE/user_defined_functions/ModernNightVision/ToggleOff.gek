int iFlag

int iSize
ref rActor

begin Function {iFlag}

	Goo1.AuxVarSetFlt "*SNV_Animation" 0

	; Disable night vision
	if eval Goo1.AuxVarGetFlt "SNV_Mode" 1 == 0
		RIMOD NVDLC03GhostNightvisionISFX

	; Disable thermal vision
	else
		RIMOD NVDLC04RedNightvisionISFX

		; Remove effect shader from all refs
		AuxTimerStop "*SNV_Thermal"
		if eval (iSize = Goo1.AuxVarSize "*SNV_Shader") > 0
			while (iSize -= 1) > -1
				if eval IsReference (rActor = Goo1.AuxVarGetRef "*SNV_Shader" iSize)
					rActor.SMS effectAbsorb
				endif
			loop
		endif
	endif

	; Play sound
	if eval iFlag
		PlaySound UIPipBoyLightOff
	endif

	; Reset aux
	Goo1.AuxVarSetFlt "SNV_Mode" 0 0
	Goo1.AuxVarErase "*SNV_Shader"
	Goo1.AuxVarErase "SNV_Armor"
	if eval Goo1.AuxVarGetFlt "*SNV_INI" 4
		Goo1.AuxVarSetFlt "SNV_Mode" 0 1
	endif

	; Reset HUD
	SetUIFloatGradual "HUDMainMenu\ModernNightVision\_AlphaStatic" 0
	SetUIFloatGradual "HUDMainMenu\ModernNightVision\_AlphaOverlay" 1 0 0.2
	SetUIFloatGradual "HUDMainMenu\ModernNightVision\_EnergyAlpha" 1 0 0.2

	; Reset power
	AuxTimerStop "*SNV_Energy"

end
