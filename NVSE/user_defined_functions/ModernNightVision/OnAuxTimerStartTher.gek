ref rOwnerForm
string_var sTimerName

int iSize
ref rActor
array_var aShaderRefs
array_var aActorRefs

begin Function {sTimerName, rOwnerForm}

	; Get cached and nearby refs
	aShaderRefs = Goo1.AuxVarGetAsArr "*SNV_Shader"
	aActorRefs = GetActorsByProcessingLevel 0

	; Process actors if any new refs found
	if aShaderRefs != aActorRefs

		; Remove effect shader from all refs
		Goo1.AuxVarErase "*SNV_Shader"
		if eval (iSize = Ar_Size aShaderRefs) > 0
			while (iSize -= 1) > -1
				if eval IsReference (rActor = aShaderRefs[iSize])
					rActor.SMS effectAbsorb
				endif
			loop
		endif

		; Add effect shader to all valid refs
		if eval (iSize = Ar_Size aActorRefs) > 0
			while (iSize -= 1) > -1
				if eval IsReference (rActor = aActorRefs[iSize]) && rActor.GetCreatureType != 6 && rActor.GetDead == 0 && rActor.GetDisabled == 0
					rActor.PMS effectAbsorb
					Goo1.AuxVarSetRef "*SNV_Shader" rActor -1
				endif
			loop
		endif

	endif

	aShaderRefs = aActorRefs = Ar_Null

end