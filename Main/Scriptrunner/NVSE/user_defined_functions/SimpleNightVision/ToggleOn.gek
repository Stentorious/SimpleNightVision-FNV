int iSize
ref rActor
array_var aShaderRefs

begin Function {}

	; Status: Off
	if Goo1.AuxVarGetFlt "SNV_Mode" 0 == 0
		Goo1.AuxVarSetFlt "SNV_Mode" 1 0

		; Enable night vision
		if Goo1.AuxVarGetFlt "SNV_Mode" 1 == 1
			IMOD NVDLC03GhostNightvisionISFX
			RIMOD NVDLC04RedNightvisionISFX
			SetUIFloatGradual "HUDMainMenu\SimpleNightVision\_Alpha" 200 (Goo1.AuxVarGetFlt "*SNV_INI" 0) 0.5
			PlaySound UIPipBoyLightOn
			SetUIFloatAlt "HUDMainMenu\SimpleNightVision\_Mode" 1
			Goo1.AuxVarSetFlt "SNV_Mode" 1 1

		; Enable thermal vision
		else
			RIMOD NVDLC03GhostNightvisionISFX
			IMOD NVDLC04RedNightvisionISFX
			SetUIFloatGradual "HUDMainMenu\SimpleNightVision\_Alpha" 200 ((Goo1.AuxVarGetFlt "*SNV_INI" 1) * 1.1) 0.5
			AuxTimerStart "*SNV_Thermal" 5 30
			PlaySound UIPipBoyLightOn
			SetUIFloatAlt "HUDMainMenu\SimpleNightVision\_Mode" 2
			Goo1.AuxVarSetFlt "SNV_Mode" 2 1
		endif

	; Status: On
	else

		; Enable night vision
		if Goo1.AuxVarGetFlt "SNV_Mode" 1 == 2

			; Remove thermal effect shader from all refs
			AuxTimerStop "*SNV_Thermal"
			aShaderRefs = Goo1.AuxVarGetAsArr "*SNV_Shader"
			if eval (iSize = Ar_Size aShaderRefs) > 0
				while (iSize -= 1) > -1
					if eval IsFormValid (rActor = aShaderRefs[iSize])
						rActor.SMS effectAbsorb
					endif
				loop
			endif

			IMOD NVDLC03GhostNightvisionISFX
			RIMOD NVDLC04RedNightvisionISFX
			SetUIFloatGradual "HUDMainMenu\SimpleNightVision\_Alpha" 200 (Goo1.AuxVarGetFlt "*SNV_INI" 0) 0.5
			PlaySound UIPipBoyLightOn
			SetUIFloatAlt "HUDMainMenu\SimpleNightVision\_Mode" 1
			Goo1.AuxVarSetFlt "SNV_Mode" 1 1
			Goo1.AuxVarErase "*SNV_Shader"

		; Enable thermal vision
		else
			RIMOD NVDLC03GhostNightvisionISFX
			IMOD NVDLC04RedNightvisionISFX
			SetUIFloatGradual "HUDMainMenu\SimpleNightVision\_Alpha" 200 ((Goo1.AuxVarGetFlt "*SNV_INI" 1) * 1.1) 0.5
			AuxTimerStart "*SNV_Thermal" 5 30
			PlaySound UIPipBoyLightOn
			SetUIFloatAlt "HUDMainMenu\SimpleNightVision\_Mode" 2
			Goo1.AuxVarSetFlt "SNV_Mode" 2 1
		endif
	endif

	aShaderRefs = Ar_Null

end