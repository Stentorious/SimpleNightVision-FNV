; Check for requirements
if Goo1.AuxVarGetFlt "*SNV_Init" == 0
	return
endif

if Goo1.AuxVarType "SNV_Mode" 0 == 0
	Goo1.AuxVarSetFlt "SNV_Mode" 0 0
	Goo1.AuxVarSetFlt "SNV_Mode" 1 1
endif
Goo1.AuxVarErase "*SNV_Shader"
SetUIFloatAlt "HudMainMenu\_SNV+A" 0
SetUIFloatAlt "HudMainMenu\_SNV+M" 0
RIMOD NVDLC03GhostNightvisionISFX
RIMOD NVDLC04RedNightvisionISFX

; Remove Cyberware vision mode
if IsModLoaded "Cyberware.esm" && GetINIFloat "General:bOverrideCyberware" "SimpleNightVision.ini" == 1
	ref rGlobal = EditorIDToFormID "CWxVisionModeLevel"
	ref rQuest = EditorIDToFormID "CWxBionicEyesQuest"
	float fGlobal = GetGlobalVariable rGlobal
	if fGlobal > 0
		if fGlobal == 1
			SetStage rQuest 2
		elseif fGlobal == 2
			SetStage rQuest 4
		elseif fGlobal == 3
			SetStage rQuest 6
		endif
		SetGlobalVariable rGlobal 0
		SetStage rQuest 0
		SetStage (EditorIDToFormID "CWxBionicEyesGrainQuest") 0
	endif
endif

; Set event handlers
SetJohnnyOnDyingEventHandler 1 (CompileScript "SimpleNightVision/OnDeath.gek") 0
SetEventHandlerAlt "OnActorUnequip" (CompileScript "SimpleNightVision/OnUnequip.gek") 1::Player
SetOnAuxTimerStopHandler 1 (CompileScript "SimpleNightVision/OnAuxTimerStop.gek") "*SNV_KeyToggle"
SetOnAuxTimerStopHandler 1 (CompileScript "SimpleNightVision/OnAuxTimerStop1.gek") "*SNV_KeySupport"
SetOnAuxTimerStartHandler 1 (CompileScript "SimpleNightVision/OnAuxTimerStartTher.gek") "*SNV_Thermal"

; Update vision mode
if Goo1.AuxVarGetFlt "SNV_Mode" 0 == 1

	; Enable night vision
	if Goo1.AuxVarGetFlt "SNV_Mode" 1 == 1
		IMOD NVDLC03GhostNightvisionISFX
		SetUIFloatAlt "HudMainMenu\_SNV+A" (Goo1.AuxVarGetFlt "*SNV_INI" 0)
		SetUIFloatAlt "HudMainMenu\_SNV+M" 1

	; Enable thermal vision
	else
		IMOD NVDLC04RedNightvisionISFX
		SetUIFloatAlt "HudMainMenu\_SNV+A" ((Goo1.AuxVarGetFlt "*SNV_INI" 1) * 1.1)
		CallAfterFrames 0 ({} => AuxTimerStart "*SNV_Thermal" 5 30)
		SetUIFloatAlt "HudMainMenu\_SNV+M" 2
	endif
endif